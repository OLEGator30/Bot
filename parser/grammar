<Prog>			->	{ <LabState> } 'EOF'
<LabState>	->	<Lab> <State>
<Lab>				->	labelname ':' | eps
<State>			->	'if' '(' <Exp> ')' <LabState> | <SimpleSt> ';' | '{' <LabState> { <LabState> } '}'
<SimpleSt>	->	'int' varname [ <Array> | <VarArg> ] { ',' varname [ <Array> | <VarArg> ] } |
								varname <Array> '=' <Exp1> |
								'goto' labname |
								'print' <ArgList> |
								'buy' <Arg2> | 'sell' <Arg2> |
								'prod' <Arg1> |
								'join' <Arg1>
								'build' <Arg0> |
								'turn' <Arg0>
<Exp>				->	<Exp1> <CompOp> <Exp1> | <Exp1>
<Exp1>			->	<Exp2> { <Op1> <Exp2> }
<Exp2>			->	<Exp3> { <Op2> <Exp3> }
<Exp3>			->	varname <Array> | number | funcname [ <Arg0> | <Arg1> ] |
								'!' <Exp3> | '-' <Exp3> | '(' <Exp> ')'
<CompOp>		->	'==' | '<' | '>'
<Op1>				->	'+' | '-' | '|'
<Op2>				->	'*' | '/' | '%' | '&'
<Arg0>			->	'(' ')'
<Arg1>			->	'(' <Exp1> ')'
<Arg2>			->	'(' <Exp1> ',' <Exp1> ')'
<VarArg>		->	'=' <Exp1> | eps
<Array>			->	'[' <Exp1> ']' | eps
<ArgList>		->	'(' <ArgPrint> { ',' <ArgPrint> } ')'
<ArgPrint>	->	string | <Exp1>
